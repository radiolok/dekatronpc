*--------------------------------------------------------------------------
* Model generated by Motega software: 
*
*              Modeling Of Tubes Employing Genetic Algorithms
*
* Models contain 1G resistors from all nodes to earth in order to avoid
* floating nodes. Triode and tetrode/pentode models contain a diode for
* simulating grid current.
*
* Non-commercial use is permitted, but at your own risk... This model
* is provided "as is", without warranty of any kind. In no event shall
* Jeroen Boschma be liable for any claim, damages or other liability,
* whether in an action of contract, tort or otherwise, arising from,
* out of or in connection with the tube model or the use or other
* dealings in the tube model.
*
*
*                         Copyright Jeroen Boschma
*                              www.boschma.com
*
* Motega V 1.0, 12-Sep-2010 23:58:18
*--------------------------------------------------------------------------

*--------------------------------------------------------------------------
* Generic tetrode/pentode model
*--------------------------------------------------------------------------

.SUBCKT TubePentode  A  Gr2  Gr1  K
+ PARAMS: ua=1 ug2=1 xa=1 xg2=1 qa=1 qg2=1 kg1=1 kg2=1 kvba=1 kvbg2=1 ce=1
*
* Resistors in order to avoid floating nodes
*
R1   A    0   1G
R2   Gr2  0   1G
R3   Gr1  0   1G
R4   K    0   1G
*
* Intermediate expressions which simplify the current calculation
*
RE1  1    0   1G
E1   1    0   VALUE={xa + ce*PWR(V(A,K),2)}
RE2  2    0   1G
E2   2    0   VALUE={V(Gr2,K)*LOG(1 + EXP(qa*(1/ua + V(Gr1,K)/V(Gr2,K))))/qa}
RE3  3    0   1G
E3   3    0   VALUE={V(Gr2,K)*LOG(1 + EXP(qg2*(1/ug2 + V(Gr1,K)/V(Gr2,K))))/qg2}
*
* Actual current calculation
*
G1   A    K   VALUE={(PWR(V(2),V(1)) + PWRS(V(2),V(1)))*ATAN(V(A,K)/kvba)/kg1}
G2   Gr2  K   VALUE={(PWR(V(3),V(1)) + PWRS(V(3),V(1)))*ATAN(V(A,K)/kvbg2)/kg2}
*
* Grid current modeling
*
RG   Gr1  4   1K
D1   4    K   DX
.MODEL DX D(IS=1N RS=1 CJO=10PF TT=1N)
*
* Close the model
*
.ENDS

*--------------------------------------------------------------------------
* Specialized tube model
*
*    Type           : 6Z43P
*    Description    : Pentode High S
*    Direct heated  : no
*    Screen present : yes
*--------------------------------------------------------------------------

.SUBCKT 6Z43P    A  Gr2  Gr1  K 
*
* Resistors in order to avoid floating nodes
*
* RF   F    0   1G
* RS   S    0   1G
*
* Link to the generic model
*
XV1     A         Gr2      Gr1      K       TubePentode
+ PARAMS: ua=76.129 ug2=8.581K xa=759.154M xg2=952.570M qa=233.302 qg2=151.489
+ kg1=68.216 kg2=142.261 kvba=19.057 kvbg2=2.179M ce=22.808U
*
* Capacitances
*
C1    K        A        3.300P   ; Cka
C2    Gr1      K        13.500P   ; Cgk
* C3    Gr1      F        0.150P   ; Cgh
C4    Gr1      A        0.075P   ; Cga
.ENDS

*--------------------------------------------------------------------------
* Specialized tube model
*
*    Type           : 6F12P
*    Description    : Pentode only
*    Direct heated  : no
*    Screen present : no
*--------------------------------------------------------------------------

.SUBCKT 6F12PP    A  Gr2  Gr1  K 
*
* Resistors in order to avoid floating nodes
*
* RF   F    0   1G
*
* Link to the generic model
*
XV1     A         Gr2      Gr1      K       TubePentode
+ PARAMS: ua=164.060 ug2=2.189K xa=442.879M xg2=687.751M qa=620.235 qg2=93.692
+ kg1=89.910 kg2=161.636 kvba=11.369 kvbg2=655.832 ce=358.645N
*
* Capacitances
*
C1    K        Gr1      6.600P   ; Ckg1
C2    K        A        1.900P   ; Cka
C3    Gr1      A        0.020P   ; Cg1a
*
* Close the model
*
.ENDS

.subckt 6BQ5_EL84_T 1 6 3
+ params: mu=18.8 ex=1.5 kg1=540 kp=165 kvb=174 rgi=1000 vct=0.01
+ ccg=10.5p cgp=4.9p ccp=6.5p
e1 7 0 value=
+{v(1,3)/kp*log(1+exp(kp*(1/mu+v(2,3)/sqrt(kvb+v(1,3)*v(1,3)))))}
re1 7 0 1g
g1 1 3 value= {(pwr(v(7),ex)+pwrs(v(7),ex))/kg1}
rcp 1 3 1g
c1 2 3 {ccg}
c2 1 2 {cgp}
c3 1 3 {ccp}
r1 2 5 {rgi}
v1 5 6 {vct}
d3 6 3 dx
.model dx d(is=1n rs=1 cjo=1pf tt=1n)
.ends

*
* AUTHOR: Totof
*
*
* 
* *
* Copyleft 2011
*
* liste des paramètres et ce que j'en ai déduit
* G = modifie la pente avant le coude pour les courbes d'anodes
* MU = modifie la pente après le coude pour les courbes d'anodes
* MU12 = modifie la hauteur du courant d'anode
* E1 = modifie l'écartement des courbes de courant d'anode 
* K1 = modifie l'angle du coude du courant d'anode
* K2 = ?
* k3 = modifie l'écartement des courbes avant le coude pour les courbes d'anodes
* k4 =  modifie la hauteur du courant d'écran
* K5 =  modifie la valeur maxi du courant d'écran pour les faibles valeurs de Va
* K6 = modifie l'angle du coude du courant d'écran
* K = modifie l'écartement des courbes de courant d'écran




.SUBCKT 6Z52P 1 2 3 4
* Anode G2 G1 Cathode
X1 1 2 3 4 PENTH1 G=.100m MU=10000 MU12=30.9 E1=4.6 k1=90.2 k2=2.1 k3=0.8 k4=85.6 k5=26.8 k6=10.6 K=4.50m
X2 3 4 Igrid ALPHA=.02m BETA=.1U
C2 1 4 1.8P
C3 3 1 .05P
C5 3 4 13.5P
RF1 1 4 200MEG
RF2 3 4 50MEG
RF3 2 4 100MEG
.ENDS





**************************************
**Modèle mathématique issu de EXCEM **
**************************************



******************
.SUBCKT PENTH1 A G2 G1 C

* Terme d'ecran
B1 10 0 V=IF(V(A,C)>0,(V(G2,C)/{MU12})*(V(A,C)-({k3}*V(G1,C)))/((V(G2,C)/{k1})+V(A,C)),0)

* Total
B2 A C I={G}*((V(G1,C)+V(10,0))+(V(A,C)/({MU}*(1-(V(G1,C)/{k2})))))**{E1}

* Calcul du courant d'ecran
B6 12 0 V=IF(V(G2,C)>0,(V(G1,C)+(V(G2,C)/{k4})),0)
B7 G2 C I={K}*(V(12,0)**1.5)*((V(A,C)+{k5})/(V(A,C)+{k6}))**3
.ENDS

******************
.SUBCKT Igrid G1 C
* Courant grille
B4 6 0 V=IF(V(G1,C)>0,{ALPHA}*V(G1,C)**1.5,{BETA}/-(V(G1,C)-.1))
B5 G1 C I=V(6,0)
.ENDS
******************

.SUBCKT 6V6 P S G K
.MODEL DX D(IS=1N RS=1 CJO=1PF TT=1N)
Esp 	2  	0  	VALUE={V(P,K)+13.49*V(S,K)+130.4*V(G,K)}
E1  	3  	2  	VALUE={5.521E-7*(PWR(V(2),1.5)+PWRS(V(2),1.5))/2}
E2  	3  	4  	VALUE={5.521E-7*PWR(13.49*V(S,K),1.5)*V(P,K)/25}
E3  	5  	4  	VALUE={(1-V(4,2)/ABS(V(4,2)+0.001))/2}
R1  	5  	0  	1.0K
Gk  	S  	K  	VALUE={V(3,2)}
Gp  	P  	S  	VALUE={0.92*(V(3,4)*(1-V(5,4))+V(3,2)*V(5,4))}
R3   	G 	10  	3k   	; FOR GRID CURRENT
D3   	10 	K	DX      ; FOR GRID CURRENT
Cgk 	G  	K  	4.5P
Cgs 	G  	S  	4.5P
Cgp 	G  	P  	0.7P
Cpk 	P 	K 	7.5P
.ENDS

.SUBCKT 6AQ5 P S G K
.MODEL DX D(IS=1N RS=1 CJO=1PF TT=1N)
Esp 	2  	0  	VALUE={V(P,K)+13.49*V(S,K)+130.4*V(G,K)}
E1  	3  	2  	VALUE={5.521E-7*(PWR(V(2),1.5)+PWRS(V(2),1.5))/2}
E2  	3  	4  	VALUE={5.521E-7*PWR(13.49*V(S,K),1.5)*V(P,K)/25}
E3  	5  	4  	VALUE={(1-V(4,2)/ABS(V(4,2)+0.001))/2}
R1  	5  	0  	1.0K
Gk  	S  	K  	VALUE={V(3,2)}
Gp  	P  	S  	VALUE={0.92*(V(3,4)*(1-V(5,4))+V(3,2)*V(5,4))}
R3   	G 	10  	3k   	; FOR GRID CURRENT
D3   	10 	K	DX      ; FOR GRID CURRENT
Cgk 	G  	K  	8P
Cgs 	G  	S  	4P
Cgp 	G  	P  	0.4P
Cpk 	P 	K 	8.5P
.ENDS

.SUBCKT 807 A S G K
Eat	at	0	VALUE={0.636*ATAN(V(A,K)/5)}
Eme	me	0	VALUE={PWR(LIMIT{V(A,K),0,2000},1.5)/1750}
Emu	mu	0	VALUE={PWRS(V(G,K),1-(LIMIT{-V(G,K),30,9999}-30)/2000)}
Egs	gs	0	VALUE={LIMIT{V(A,K)/2.5+V(S,K)*15.15+V(mu)*134,0,1E6}}
Egs2	gs2	0	VALUE={PWRS(V(gs),1.5)*0.8E-6}
Ecath  	cc    	0 	VALUE={LIMIT{V(gs2)*V(at),0,V(me)}}
Ga     	A     	K     	VALUE={V(cc)}
Escrn	sc	0	VALUE={0.76*V(gs2)*(1.1-V(at))}
Gs     	S     	K	VALUE={V(sc)*LIMIT{V(S,K),0,10}/10}
Gg	G	K	VALUE={PWR(LIMIT{V(G,K)+1,0,1E6},1.5)*(1.25-V(at))*650E-6}
Cg1	G	K	7.5p
Cgs	G	S	5.0p
Cak	A	K	8.2p
Cg1a	G	A	0.2p
.ENDS 

.SUBCKT EL803 A S G K
Eat at 0 VALUE={limit(0.636*ATAN(limit(V(A,K),0,200)/5.39935952007373),0,1e6)} ;arctangent shaping
Eme me 0 VALUE={0.000459037814532166*PWR(V(A,K),1.19017332861348)} ; diodeline
Egs gs1 0 VALUE={LIMIT(V(A,K)/12216.2805361082+V(S,K)/21.6080182096161+V(G,K)/0.878756035095985 ,0,1E6)}  ;the basic voltage
Egs2 gs2 0 VALUE={PWRS(V(gs1),1.48413876122404)*0.0030939788209251}  ;raise to the power and mult by perveance
Ga A K  VALUE={limit(V(gs2)*V(at),0,V(me))}  ; anode current limited per diode line
Gs S K  VALUE={1.06934398863015*V(gs2)*(1.1-V(at))}; screen current, reverse arctangent shaping
Gg G K VALUE={PWR(LIMIT(V(G,K)+-0.2500372391271 ,0,1E6),1.5)*(1.25-V(at))*-0.00098104133922726}  ; grid current
C1 G K 0.15p
C2 A K 8p
C3 G A 0.12p
C4 G S 10.4p
.ENDS EL803
